plugins {
    alias(libs.plugins.android.application)
    // id 'com.android.application'
    alias(libs.plugins.kotlin.android)
    // id 'org.jetbrains.kotlin.android'
    alias(libs.plugins.compose.compiler)
    // id 'org.jetbrains.kotlin.plugin.compose'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
    id 'kotlin-kapt'
    id 'com.google.devtools.ksp'
    id 'androidx.room'
}

android {
    signingConfigs {
        config {
            keyAlias 'chaolee'
            keyPassword '86637971'
            storeFile file('/home/chaolee/AndroidStudioProjects/keystore/key.jks')
            storePassword '86637971'
        }
    }

    compileSdk = 36

    defaultConfig {
        minSdk = 23
        targetSdk = 36
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        signingConfig signingConfigs.config
        vectorDrawables {
            useSupportLibrary true
        }
    }

    flavorDimensions += ["product"]
    productFlavors {
        create("colorballs") {
            dimension = "product"
            versionCode 7963
            versionName "7.963"
            applicationId = "com.smile.colorballs"
            isDefault = true
        }
        create("ballsremover") {
            dimension = "product"
            versionCode 163
            versionName "1.63"
            applicationId = "com.smile.ballsremover"
        }
    }

    sourceSets {
        main {
            java.srcDirs += ["src/main/java"]
            res.srcDirs += ["src/main/res"]
            manifest {
                srcFile "src/main/AndroidManifest.xml"
            }
        }

        colorballs {
            java.srcDirs += ["src/colorballs/java", "src/ballsremover/java"]
            res.srcDirs += ["src/colorballs/res", "src/ballsremover/res"]
            manifest {
                srcFile "src/colorballs/AndroidManifest.xml"
            }
        }

        ballsremover {
            java.srcDirs += ["src/ballsremover/java"]
            res.srcDirs += ["src/ballsremover/res"]
            manifest {
                srcFile "src/ballsremover/AndroidManifest.xml"
            }
        }
    }
    
    buildFeatures  {
        dataBinding true    // viewBinding true
        buildConfig true
        compose true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // signingConfig signingConfigs.config
        }
        debug {
            minifyEnabled false
            // signingConfig signingConfigs.config
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    /*
    kotlinOptions {
        jvmTarget = '17'    // deprecated
        // for default fun of Kotlin
        freeCompilerArgs = ['-Xjvm-default=all']    // deprecated
    }
    */

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.5.15'
    }

    namespace 'com.smile.colorballs_main'
    testNamespace 'com.smile.colorballs_main.androidTest'

    // Add this block here and smilelibraries module
    room {
        schemaDirectory "$projectDir/schemas"
    }
}

allprojects {
    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
    configurations.configureEach {
        resolutionStrategy {
            force("com.google.auto.value:auto-value-annotations:1.6.3")
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation libs.androidx.appcompat
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.recyclerview
    implementation libs.audience.network.sdk
    implementation libs.google.ads.mediation.facebook
    // InMobi's SDK
    implementation(libs.inmobi.ads.kotlin.sdk)
    // AdMob's InMobi adapter
    implementation(libs.google.ads.mediation.inmobi)
    // ironSource's SDK
    implementation(libs.ironsource.mediation.sdk)
    // implementation("com.unity3d.ads-mediation:mediation-sdk:9.0.0")
    // AdMob's ironSource adapter
    implementation(libs.google.ads.mediation.ironsource)

    implementation libs.infer.annotation
    implementation libs.firebase.core
    implementation libs.firebase.ads
    implementation libs.androidx.multidex
    implementation libs.core.ktx
    implementation(libs.kotlinx.coroutines.core)
    implementation(libs.kotlinx.coroutines.android)

    implementation libs.androidx.lifecycle.livedata.ktx
    implementation libs.androidx.lifecycle.viewmodel.ktx
    implementation libs.androidx.fragment.ktx
    implementation libs.androidx.lifecycle.runtime.ktx

    implementation libs.androidx.activity.compose
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.runtime
    implementation libs.androidx.ui
    implementation libs.androidx.ui.graphics
    implementation libs.androidx.ui.tooling.preview
    implementation libs.androidx.material3
    implementation libs.accompanist.drawablepainter
    implementation libs.billing.ktx

    implementation(libs.androidx.room.runtime)
    // To use Kotlin Symbol Processing (KSP)
    ksp(libs.androidx.room.compiler)
    // optional - Kotlin Extensions and Coroutines support for Room
    implementation(libs.androidx.room.ktx)

    implementation project(':smilelibraries')
    implementation project(':NativeTemplatesModels')

    testImplementation libs.junit
    testImplementation libs.androidx.espresso.core
    testImplementation libs.robolectric
    testImplementation libs.kotlinx.coroutines.test
    testImplementation libs.mockito.core
    // optional - Test helpers
    testImplementation(libs.androidx.room.testing)
    // testImplementation "org.mockito.kotlin:mockito-kotlin:4.8.0"
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.rules
    androidTestImplementation libs.androidx.truth
    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation libs.androidx.uiautomator
    androidTestImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation libs.androidx.ui.test.junit4
    debugImplementation libs.androidx.ui.tooling
    debugImplementation libs.androidx.ui.test.manifest
}